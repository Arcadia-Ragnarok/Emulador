-	script	Unidentified Creature#i	FAKE_NPC,{
	end;
	OnInit:
	switch(atoi(strnpcinfo(NPC_NAME_HIDDEN))) {
		case 1: setarray .@npc$,"01","02","03","04"; break;
		case 5: setarray .@npc$,"05","06","07","08"; break;
		case 9: setarray .@npc$,"09","10","11"; break;
		default: end;
	}
	.@size = getarraysize(.@npc$);
	.@rand = rand(.@size);
	for(.@i = 0; .@i<.@size; ++.@i)
		donpcevent "Unidentified Creature#"+.@npc$[.@i]+"::"+((.@i == .@rand)?"OnEnable":"OnDisable");
	end;
	OnEnable:
	enablenpc strnpcinfo(NPC_NAME);
	end;
	OnDisable:
	disablenpc strnpcinfo(NPC_NAME);
	end;
	OnBingx2:
	switch(atoi(strnpcinfo(NPC_NAME_HIDDEN))) {
		case 1: setarray .@npc$,"02","03","04"; break;
		case 5: setarray .@npc$,"06","07","08"; break;
		case 9: setarray .@npc$,"10","11"; break;
	}
	.@size = getarraysize(.@npc$);
	.@rand = rand(.@size);
	for(.@i = 0; .@i<.@size; ++.@i)
		donpcevent "Unidentified Creature#"+.@npc$[.@i]+"::"+((.@i == .@rand)?"OnEnable":"OnDisable");
	donpcevent strnpcinfo(NPC_NAME)+"::OnDisable";
	end;
	OnTouch:
	if (getcharid(CHAR_ID_PARTY)) {
		if ($ma_name04$ == strcharinfo(PC_NAME) || $ma_name05$ == strcharinfo(PC_NAME) || $ma_name06$ == strcharinfo(PC_NAME)) {
			donpcevent strnpcinfo(NPC_NAME)+"::OnBingx2";
			mes "The unidentified creature gets a glimpse of you, blushes and then disappears.";
			close;
		}
		if (rand(1,5)%2) {
			switch(atoi(strnpcinfo(NPC_NAME_HIDDEN))) {
				case 1: $ma_name04$ = strcharinfo(PC_NAME); break;
				case 5: $ma_name05$ = strcharinfo(PC_NAME); break;
				case 9: $ma_name06$ = strcharinfo(PC_NAME); break;
			}
		}
		donpcevent strnpcinfo(NPC_NAME)+"::OnBingx2";
		mes "The unidentified creature gets a glimpse of you and disappears.";
		close;
	}
	donpcevent strnpcinfo(NPC_NAME)+"::OnBingx2";
	mes "The unidentified creature looks at you and runs away.";
	close;
}
ma_fild01,74,367,6	duplicate(Unidentified Creature#i)	Unidentified Creature#01	4_MAL_BUDIDAI,2,2
ma_fild02,282,41,4	duplicate(Unidentified Creature#i)	Unidentified Creature#05	4_MAL_BUDIDAI,2,2
ma_scene01,195,92,4	duplicate(Unidentified Creature#i)	Unidentified Creature#09	4_MAL_BUDIDAI,2,2

-	script	Unidentified Creature#j	FAKE_NPC,{
	end;
	OnEnable:
	enablenpc strnpcinfo(NPC_NAME);
	end;
	OnDisable:
	disablenpc strnpcinfo(NPC_NAME);
	end;
	OnBingx2:
	switch(atoi(strnpcinfo(NPC_NAME_HIDDEN))) {
		case 2: setarray .@npc$,"01","03","04"; break;
		case 3: setarray .@npc$,"01","02","04"; break;
		case 4: setarray .@npc$,"01","02","03"; break;
		case 6: setarray .@npc$,"05","07","08"; break;
		case 7: setarray .@npc$,"05","06","08"; break;
		case 8: setarray .@npc$,"05","06","07"; break;
		case 10: setarray .@npc$,"09","11"; break;
		case 11: setarray .@npc$,"09","10"; break;
	}
	.@size = getarraysize(.@npc$);
	.@rand = rand(.@size);
	for(.@i = 0; .@i<.@size; ++.@i)
		donpcevent "Unidentified Creature#"+.@npc$[.@i]+"::"+((.@i == .@rand)?"OnEnable":"OnDisable");
	donpcevent strnpcinfo(NPC_NAME)+"::OnDisable";
	end;
	OnTouch:
	.@i = atoi(strnpcinfo(NPC_NAME_HIDDEN));
	if (.@i%4 == 2) {
		if (getcharid(CHAR_ID_CHAR) == getpartyleader(getcharid(CHAR_ID_PARTY),2))
			.@pass = 1;
	} else if (.@i%4 == 3) {
		if (questprogress(4229,PLAYTIME) && questprogress(9223,PLAYTIME) && questprogress(12278,PLAYTIME))
			.@pass = 1;
	} else if (.@i%4 == 0) {
		if (rand(1,100)%10 == 7)
			.@pass = 1;
	}
	if (.@pass) {
		if ($ma_name04$ == strcharinfo(PC_NAME) || $ma_name05$ == strcharinfo(PC_NAME) || $ma_name06$ == strcharinfo(PC_NAME)) {
			donpcevent strnpcinfo(NPC_NAME)+"::OnBingx2";
			mes "The unidentified creature gets a glimpse of you, blushes and then disappears.";
			close;
		}
		if (rand(1,5)%2) {
			switch(.@i) {
				case 2:
				case 3:
				case 4:
				$ma_name04$ = strcharinfo(PC_NAME);
				break;
				case 6:
				case 7:
				case 8:
				$ma_name05$ = strcharinfo(PC_NAME);
				break;
				case 10:
				case 11:
				$ma_name06$ = strcharinfo(PC_NAME);
				break;
			}
		}
		donpcevent strnpcinfo(NPC_NAME)+"::OnBingx2";
		mes "The unidentified creature gets a glimpse of you and disappears.";
		close;
	}
	donpcevent strnpcinfo(NPC_NAME)+"::OnBingx2";
	mes "The unidentified creature looks at you and runs away.";
	close;
}
ma_fild01,109,116,4	duplicate(Unidentified Creature#j)	Unidentified Creature#02	4_MAL_BUDIDAI,2,2
ma_fild01,280,150,6	duplicate(Unidentified Creature#j)	Unidentified Creature#03	4_MAL_BUDIDAI,2,2
ma_fild01,309,221,6	duplicate(Unidentified Creature#j)	Unidentified Creature#04	4_MAL_BUDIDAI,2,2
ma_fild02,246,324,4	duplicate(Unidentified Creature#j)	Unidentified Creature#06	4_MAL_BUDIDAI,2,2
ma_fild02,71,296,6	duplicate(Unidentified Creature#j)	Unidentified Creature#07	4_MAL_BUDIDAI,2,2
ma_fild02,32,263,4	duplicate(Unidentified Creature#j)	Unidentified Creature#08	4_MAL_BUDIDAI,2,2
ma_scene01,158,139,4	duplicate(Unidentified Creature#j)	Unidentified Creature#10	4_MAL_BUDIDAI,2,2
ma_scene01,167,112,6	duplicate(Unidentified Creature#j)	Unidentified Creature#11	4_MAL_BUDIDAI,2,2
